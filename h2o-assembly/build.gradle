apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

description = "H2O Application Assembly"

dependencies {
  compile project(":h2o-app")
  compile project(":h2o-persist-s3")
  compile project(":h2o-persist-hdfs")
  if (project.hasProperty("doIncludeOrc") && project.doIncludeOrc == "true") {
    compile project(":h2o-orc-parser")
  }
  if (doIncludeScalaAssembly()){
    compile project(":h2o-scala_"+scalaVersion())
    compile "org.scala-lang:scala-library:"+scalaVersion()

  }
  compile "org.slf4j:slf4j-log4j12:1.7.5"
}

shadowJar {
  mergeServiceFiles()
  classifier = ''
  // CDH 5.3.0 provides joda-time v1.6 which is too old, shadow the library instead
  if (!project.hasProperty("jacocoCoverage")) {
    relocate 'org.joda.time', 'ai.h2o.org.joda.time'
  }
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.SF'
  exclude 'synchronize.properties'
  exclude 'uploader.properties'
  exclude 'test.properties'
  exclude 'cockpitlite.properties'
  exclude 'devpay_products.properties'
  manifest {
    attributes 'Main-Class': 'water.H2OApp'
  }
}

def doIncludeScalaAssembly() {
  detectProperty("doIncludeScalaAssembly", "false").toBoolean()
}

def scalaVersion(){
  def scalaVersion = detectProperty("scalaVersion", "2.10")
  def scalaBaseVersion = scalaVersion.substring(0,4)
  if(!["2.10", "2.11"].contains(scalaBaseVersion)){
    throw new IllegalArgumentException("Supported scala base versions are 2.10 and 2.11")
  }
  return scalaBaseVersion
}

def detectProperty(String propertyName, String defaultValue) {
  String value = [ project.hasProperty(propertyName) ? project[propertyName] : null,
                                    System.properties[propertyName],
                                    defaultValue
  ].find { h -> h!=null } // first match
  // Return env
  return value
}

artifacts {
  archives shadowJar
}

//
// Support make infrastructure by copying the resulting assembly into parent
// project build directory
//

def assembly = "h2o-assembly.jar"
def allInOne = "h2o.jar"

task copyJar(type: Copy) {
    from ("${project.buildDir}/libs"){
        include assembly
    }
    into "${project.parent.buildDir}"
    rename { it.replace(assembly, allInOne) }
}
// Execute always copyJar
shadowJar.finalizedBy copyJar
// Run shadowJar as par of build
jar.finalizedBy shadowJar

